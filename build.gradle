plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.jfrog.bintray' version '1.7.3'
}

group 'com.ringcentral'
version '0.2.0'

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.31'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.7.0'
    compile group: 'com.pubnub', name: 'pubnub-gson', version: '4.6.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'com/ringcentral/definitions/**',
                'com/ringcentral/paths/**'
            ])
        })
    }
}

bintray {
    user = property('user')
    key = property('key')
    configurations = ['archives']
    pkg {
        repo = 'generic'
        name = 'ringcentral'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/tylerlong/ringcentral-java.git'
        version {
            name = '0.2.0'
            desc = 'RingCentral Java SDK 0.2.0'
            released = new Date()
            vcsTag = '0.2.0'
        }
    }
}
